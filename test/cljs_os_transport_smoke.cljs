;; nbb smoke: dual-runtime OS transports (ADR 0147)
;;   nbb --classpath "src:$(clojure -Spath)" test/cljs_os_transport_smoke.cljs
(ns cljs-os-transport-smoke
  (:require [clojure.string :as str]
            [provider.process :as process]
            [provider.process-transport :as process-transport]
            [provider.scoped-fs :as scoped-fs]
            [provider.scoped-fs-transport :as scoped-fs-transport]))

(defn- fail! [msg]
  (js/console.error (str "FAIL: " msg))
  (js/process.exit 1))

(defn- assert! [cond msg]
  (when-not cond (fail! msg)))

(defn- echo-bin []
  (let [fs (js/require "fs")]
    (cond
      (.existsSync fs "/bin/echo") "/bin/echo"
      (.existsSync fs "/usr/bin/echo") "/usr/bin/echo"
      :else (fail! "no echo binary"))))

(defn- tmp-dir []
  (let [fs (js/require "fs")
        os (js/require "os")
        path (js/require "path")
        dir (.join path (.tmpdir os) (str "kotoba-cljs-fs-" (js/Date.now)))]
    (.mkdirSync fs dir)
    dir))

(defn -main []
  ;; process os-spawn via real spawnSync
  (let [bin (echo-bin)
        spawn (process-transport/os-spawn {:binaries {"echo" bin}})
        ps (:providers (process/create-providers
                        {:allowed-commands #{"echo"}
                         :spawn spawn}))
        p (get ps process/capability-id)
        reply ((:invoke p) [process/spawn-request-type ["echo" "hello-cljs"] 4096 5000])]
    (assert! (= :ok (second reply)) (str "spawn tag " reply))
    (let [[_ exit stdout _] (nth reply 2)]
      (assert! (zero? exit) (str "exit " exit))
      (assert! (str/includes? (str stdout) "hello-cljs") (str "stdout " stdout))))
  ;; unmapped binary
  (let [spawn (process-transport/os-spawn {:binaries {"echo" (echo-bin)}})
        r (spawn {:argv ["rm" "-rf" "/"] :max-stdout-bytes 100 :timeout-ms 1000})]
    (assert! (= :error (:tag r)) "unmapped should error")
    (assert! (= :process/no-binary (:code r)) "unmapped code"))
  ;; scoped-fs os-store roundtrip
  (let [fs (js/require "fs")
        dir (tmp-dir)
        store (scoped-fs-transport/os-store {:roots {:cache/tmp dir}})
        ps (:providers (scoped-fs/create-providers
                        {:allowed-roots #{:cache/tmp}
                         :store store}))
        p (get ps scoped-fs/capability-id)
        write-req [scoped-fs/request-type :write
                   [scoped-fs/write-request-type :cache/tmp "nested/hello.txt" "cljs-hi"]]
        read-req [scoped-fs/request-type :read
                  [scoped-fs/read-request-type :cache/tmp "nested/hello.txt"]]
        escape [scoped-fs/request-type :read
                [scoped-fs/read-request-type :cache/tmp "../outside"]]]
    (assert! (= :written (second ((:invoke p) write-req))) "write")
    (let [found ((:invoke p) read-req)]
      (assert! (= :content (second found)) "read tag")
      (assert! (= "cljs-hi" (nth found 2)) "read value"))
    (assert! (= :error (second ((:invoke p) escape))) "escape denied")
    ;; cleanup
    (try
      (.rmSync fs dir #js {:recursive true :force true})
      (catch :default _ nil)))
  (js/console.log "cljs OS transport smoke OK")
  (js/process.exit 0))

(-main)
