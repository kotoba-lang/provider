;; nbb smoke: git cljs os-run (ADR 0150)
;;   nbb --classpath "src:$(clojure -Spath)" test/cljs_git_os_run_smoke.cljs
(ns cljs-git-os-run-smoke
  (:require [clojure.string :as str]
            [provider.git :as git]
            [provider.git-transport :as git-transport]))

(defn- fail! [msg]
  (js/console.error (str "FAIL: " msg))
  (js/process.exit 1))

(defn- assert! [cond msg]
  (when-not cond (fail! msg)))

(defn- git-bin []
  (let [fs (js/require "fs")]
    (cond
      (.existsSync fs "/usr/bin/git") "/usr/bin/git"
      (.existsSync fs "/bin/git") "/bin/git"
      (.existsSync fs "/opt/homebrew/bin/git") "/opt/homebrew/bin/git"
      :else (fail! "no absolute git binary"))))

(defn- worktree []
  (let [path (js/require "path")
        fs (js/require "fs")
        ;; run smoke from provider repo root (cwd)
        root (.resolve path (js/process.cwd))]
    (assert! (.existsSync fs (.join path root ".git")) "no .git at provider root")
    root))

(defn -main []
  ;; inject spawnSync double
  (let [calls (atom [])
        spawn-sync (fn [bin args opts]
                     (swap! calls conj {:bin bin :args (js->clj args) :cwd (.-cwd opts)})
                     #js {:status 0 :stdout "main\n" :stderr "" :error nil})
        run (git-transport/os-run {:git-bin "/usr/bin/git"
                                   :worktree "/tmp/repo"
                                   :spawn-sync spawn-sync})
        reply (run {:args ["rev-parse" "--abbrev-ref" "HEAD"]
                    :max-stdout-bytes 4096
                    :timeout-ms 5000})]
    (assert! (= :ok (:tag reply)) (str "inject reply " reply))
    (assert! (= 0 (:exit reply)) "exit")
    (assert! (str/includes? (:stdout reply) "main") "stdout")
    (assert! (= "/usr/bin/git" (:bin (first @calls))) "bin")
    (assert! (= "/tmp/repo" (:cwd (first @calls))) "cwd")
    (assert! (= ["rev-parse" "--abbrev-ref" "HEAD"] (:args (first @calls))) "args"))
  ;; real git when available
  (let [bin (git-bin)
        wt (worktree)
        run (git-transport/os-run {:git-bin bin :worktree wt})
        ps (:providers (git/create-providers {:run run}))
        p (get ps git/capability-id)
        reply ((:invoke p) [git/run-request-type
                            ["rev-parse" "--abbrev-ref" "HEAD"]
                            4096 5000])]
    (assert! (= :ok (second reply)) (str "live reply " reply))
    (let [[_ exit stdout _] (nth reply 2)]
      (assert! (zero? exit) (str "exit " exit))
      (assert! (pos? (count (str/trim (str stdout)))) "branch name")))
  (js/console.log "cljs git os-run smoke OK")
  (js/process.exit 0))

(-main)
