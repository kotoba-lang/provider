(ns provider.state
  "Bounded deterministic reference provider for :state/transact v1.

  Entry `:version` is an `:i64` ABI field. On `:cljs` the canonical
  representation is JS `bigint` (same rule ADR 0073 / provider#2 / #3
  applied to clock, log, and http). Plain `inc` on a number fails
  `typed-cap-call` result validation with `invalid-parametric-value`."
  (:require [kotoba.kir.value :as value]
            #?@(:cljs [[kotoba.kir.cljs-i64 :as i64]])))

(def capability-id 8)
(def max-entries 256)

(def get-type [:record :kotoba.state/get [[:key :keyword]]])
(def put-type [:record :kotoba.state/put [[:key :keyword] [:value :string]]])
(def delete-type [:record :kotoba.state/delete [[:key :keyword]]])
(def request-type
  [:variant :kotoba.state/request
   [[:get get-type] [:put put-type] [:delete delete-type]]])

(def entry-type
  [:record :kotoba.state/entry [[:key :keyword] [:value :string] [:version :i64]]])
(def error-type
  [:record :kotoba.state/error [[:code :keyword] [:message :string]]])
(def result-type
  [:variant :kotoba.state/result
   [[:found entry-type] [:missing :bool] [:written entry-type]
    [:deleted :bool] [:error error-type]]])

(def schemas
  {:kotoba.state/get get-type
   :kotoba.state/put put-type
   :kotoba.state/delete delete-type
   :kotoba.state/request request-type
   :kotoba.state/entry entry-type
   :kotoba.state/error error-type
   :kotoba.state/result result-type})

(defn- result [tag payload]
  [result-type tag payload])

(defn- entry [key {:keys [value version]}]
  [entry-type key value version])

(defn- initial-version
  "First version number assigned to an entry in a fresh instance with
  `initial-count` pre-seeded keys (each seed gets version 1; next free
  counter starts at initial-count+1)."
  [initial-count]
  #?(:clj (inc initial-count)
     :cljs (+ (i64/->bigint initial-count) i64/one)))

(defn- next-version! [version-atom]
  (swap! version-atom #?(:clj inc :cljs (fn [n] (+ n i64/one)))))

(defn- seed-version []
  #?(:clj 1 :cljs i64/one))

(defn provider
  "Creates one isolated bounded state provider. State is host-owned and is
  observable by guest code only through the typed request/result contract."
  ([] (provider {}))
  ([initial]
   (when-not (and (map? initial) (<= (count initial) max-entries)
                  (every? keyword? (keys initial)) (every? string? (vals initial)))
     (throw (ex-info "invalid initial state" {:phase :state-provider})))
   (doseq [[key text] initial]
     (value/bounded-keyword! key value/keyword-value-byte-limit)
     (value/bounded-string! text value/string-value-byte-limit))
   (let [seed-v (seed-version)
         cells (atom (into {} (map (fn [[key text]]
                                     [key {:value text :version seed-v}]))
                           initial))
         next-version (atom (initial-version (count initial)))]
     {:request-type request-type
      :result-type result-type
      :invoke
      (fn [[actual-type operation payload]]
        (when-not (= actual-type request-type)
          (throw (ex-info "state request contract mismatch" {:phase :state-provider})))
        (case operation
          :get
          (let [[_ key] payload]
            (if-let [stored (get @cells key)]
              (result :found (entry key stored))
              (result :missing false)))

          :put
          (let [[_ key text] payload]
            (value/bounded-string! text value/string-value-byte-limit)
            (if (and (not (contains? @cells key)) (>= (count @cells) max-entries))
              (result :error [error-type :state/capacity "state entry limit reached"])
              (let [version (next-version! next-version)
                    stored {:value text :version version}]
                (swap! cells assoc key stored)
                (result :written (entry key stored)))))

          :delete
          (let [[_ key] payload
                present? (contains? @cells key)]
            (swap! cells dissoc key)
            (result :deleted present?))

          (throw (ex-info "unknown state operation" {:phase :state-provider
                                                       :operation operation}))))})))
